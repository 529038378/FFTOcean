// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma kernel PostKernel

# include "MathUtil.hlsl"

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> HeightPingTex;
RWTexture2D<float4> HeightPongTex;
RWTexture2D<float4> DisplacePingTex;
RWTexture2D<float4> DisplacePongTex;
RWTexture2D<float4> NormalResTex;
RWTexture2D<float4> LutTex;
RWTexture2D<float4> PostInputTex;
RWTexture2D<float4> JacobResTex;
int Ping;
RWTexture2D<float4> DebugTex;

uint GroupSize;
uint Stage;
float Length;
uint TotalStageCount;
uint Size;
int ComputeLine;

float CalSymbol(uint2 id)
{ 
    int exp = (int)id.x + (int)id.y - (int)Size / 2;
    return (exp % 2 != 0) ? -1 : 1;
    //pow 不支持底为复数
    //return pow(-1, id.x * 1.0f - Size * 0.5f + id.y * 1.0f);
}

float4 CalDisplaceKModule(float2 k, float4 input)
{
    float k_mag = CalMagnitude(k);
    if (k_mag < 0.0001)
    {
        return float4(0, 0, 0, 0);
    }
    float2 module = k / k_mag;
    float4 res;
    res.x = module.x * input.z; 
    res.y = -1 * module.x * input.x;
    res.z = module.y * input.z;
    res.w = -1 * module.y * input.x;
    return res;
}

float4 CalNormalModule(float2 k, float4 input)
{
    //取实部作为对应的值
    float4 res;
    res.x = -1 * (k.x * input.z);
    res.y = k.x * input.x;
    res.z = -1 * (k.y * input.z);
    res.w = k.y * input.x;
    return res;
}

float4 CalSum(float2 self, float2 pat, float2 weight, float reverse)
{
    float2 mul = -1 == reverse ? self : pat;
    float2 non_mul = -1 == reverse ? pat : self;
    float2 weight_pat_mul_com = MulComplex(weight, mul);
    float real = reverse * weight_pat_mul_com.x + non_mul.x;
    float ima = reverse * weight_pat_mul_com.y + non_mul.y;
    return float4(real, 0, ima, 1);
}

float4 Cal2Sum(float4 selfs, float4 pats, float2 weight, float reverse)
{
    float4 x_sum = CalSum(float2(selfs.x, selfs.y), float2(pats.x, pats.y), weight, reverse);
    float4 z_sum = CalSum(float2(selfs.z, selfs.w), float2(pats.z, pats.w), weight, reverse);
    return float4(x_sum.x, x_sum.z, z_sum.x, z_sum.z);
}

float3 NormalizeVector(float3 normal)
{
    float length = CalMagnitudeVec3(normal);
    return normal / length;
}

float4 CalJacobDxxDzz(float2 cor, float2 input)
{
    float cor_mag = CalMagnitude(cor);
    float2 Dxx = cor.x * cor.x / cor_mag * input; 
    float2 Dzz = cor.y * cor.y / cor_mag * input;
    return float4(Dxx.y, -Dxx.x, Dzz.y, -Dzz.x);      
}

float4 CalJacobDxzDzx(float2 cor, float2 input)
{
    float cor_mag = CalMagnitude(cor);
    float2 D = cor.x * cor.y / cor_mag * input;
    return float4(D.y, -D.x, D.y, -D.x);
}

[numthreads(8, 8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint2 cor = id.xy;
    cor.y = Stage;
    cor.x = 1 == ComputeLine ? id.x : id.y;
    uint2 pat_cor;
    float4 weight = LutTex[cor];
    int reverse = (cor.x % GroupSize) >= (GroupSize / 2) ? -1 : 1;
    uint2 real_index = id.xy;
    if(1 == ComputeLine)
    {
        real_index.x = (0 == Stage) ? CalBitReverse(id.x, TotalStageCount) : id.x;
        pat_cor.x = (0 == Stage) ? CalBitReverse(id.x + reverse, TotalStageCount) : (id.x + reverse * (int)GroupSize/2);
        pat_cor.y = id.y;
    }
    else
    {
        real_index.y = (0 == Stage) ? CalBitReverse(id.y, TotalStageCount) : id.y;
        pat_cor.x = id.x;
        pat_cor.y = (0 == Stage) ? CalBitReverse(id.y + reverse, TotalStageCount) : (id.y + reverse * (int)GroupSize/2);
    }

    float4 self_input = (1 == Ping) ? HeightPingTex[real_index] : HeightPongTex[real_index];
    float4 pat_input = (1 == Ping) ? HeightPingTex[pat_cor] : HeightPongTex[pat_cor];
    float4 self_displace_input = (1 == Ping) ? DisplacePingTex[real_index] : DisplacePongTex[real_index];
    float4 pat_displace_input = (1 == Ping) ? DisplacePingTex[pat_cor] : DisplacePongTex[pat_cor];
    
    //符号校正
    if(1 == ComputeLine && 0 == Stage)
    {
        float neg = real_index.x % 2 == 0 ? 1 : -1;
        self_input *= neg;
        self_displace_input *= neg;

        neg = pat_cor.x % 2 == 0 ? 1 : - 1;
        pat_input *= neg;
        pat_displace_input *= neg;
    }
    else if(0 == ComputeLine && 0 == Stage)
    {
        float neg = CalSymbol(real_index);
        self_input *= neg;
        self_displace_input *= neg;

        neg = CalSymbol(pat_cor.xy);
        pat_input *= neg;
        pat_displace_input *= neg;
    }
    //高度图计算
    float2 weight_com = float2(weight.x, weight.z);
    float2 pat_com = float2(pat_input.x, pat_input.z);
    float2 self_com = float2(self_input.x, self_input.z);
    if(1 == Ping)
    {
        HeightPongTex[id.xy] = CalSum(self_com, pat_com, weight_com, reverse );
    }
    else
    {
        HeightPingTex[id.xy] = CalSum(self_com, pat_com, weight_com, reverse );
    }
    //displace计算
    float2 k_cor = float2(2.0f * PI * ((int)real_index.x - (int)Size/2)/Length, 2.0f * PI * ((int)real_index.y - (int)Size/2)/Length);
    float2 pat_k_cor = float2(2.0f * PI * ((int)pat_cor.x - (int)Size/2)/Length, 2.0f * PI *((int)pat_cor.y - (int)Size/2)/Length);
    float4 displace_self_com = (0 == Stage && 1 == ComputeLine ) ? CalDisplaceKModule(k_cor, self_displace_input) : self_displace_input;
    float4 displace_pat_com = (0 == Stage && 1 == ComputeLine) ? CalDisplaceKModule(pat_k_cor, pat_displace_input) : pat_displace_input;
    float4 displace_x_complex = CalSum(displace_self_com.xy, displace_pat_com.xy, weight_com, reverse);
    float4 displace_z_complex = CalSum(displace_self_com.zw, displace_pat_com.zw, weight_com, reverse);
    if(1 == Ping)
    {
        DisplacePongTex[id.xy] = float4(displace_x_complex.x, displace_x_complex.z, displace_z_complex.x, displace_z_complex.z);    
    }
    else
    {
        DisplacePingTex[id.xy] = float4(displace_x_complex.x, displace_x_complex.z, displace_z_complex.x, displace_z_complex.z);   
    }

    //最后的符号校正
    if(0 == ComputeLine && (TotalStageCount - 1) == Stage)
    {
        float neg = (((int)real_index.y - (int)Size / 2 )% 2 == 0) ? 1 : -1;
        if(1 == Ping)
        {
            HeightPongTex[id.xy] *= neg;
            DisplacePongTex[id.xy] *= neg;
        }
        else
        {
            HeightPingTex[id.xy] *= neg;
            DisplacePingTex[id.xy] *= neg;
        }
        HeightPingTex[id.xy] = float4(HeightPingTex[id.xy].x, 0, HeightPingTex[id.xy].z, 1);
        HeightPongTex[id.xy] = float4(HeightPongTex[id.xy].x, 0, HeightPongTex[id.xy].z, 1);
    }
}

float2 CheckRange(float2 input)
{
    input.x %= GroupSize;
    input.y %= GroupSize;
    input.x += input.x < 0 ? GroupSize : 0;
    input.y += input.y < 0 ? GroupSize : 0;
    return input;
}

[numthreads(8,8,1)]
void PostKernel (uint3 id : SV_DispatchThreadID)
{
    //jacob post
    float2 right_index = CheckRange(id.xy + float2(1, 0));
    float2 left_index = CheckRange(id.xy - float2(1, 0));
    float2 up_index = CheckRange(id.xy + float2(0, 1));
    float2 down_index = CheckRange(id.xy - float2(0, 1));

    float2 dDdy =  0.5 * (PostInputTex[up_index].xz - PostInputTex[down_index].xz) / 30;
    float2 dDdx = 0.5 * (PostInputTex[right_index].xz - PostInputTex[left_index].xz) / 30;
    float jacob = (1 + dDdy.y) * (1 + dDdx.x) - dDdx.y * dDdy.x;
    jacob = max(0, 1 - jacob);
    /*jacob = 1 + 3 * smoothstep(1.2, 1.8, jacob);
    jacob = min(jacob, 1);
    jacob = smoothstep(0, 1, jacob);*/
    JacobResTex[id.xy] = float4(jacob, jacob, jacob, 1);
    //DebugTex[id.xy] = float4(JacobDxxDzzInput.x, 0, JacobDxxDzzInput.y,1);

    //normal post
    float dx = 0.5 * (PostInputTex[right_index].x - PostInputTex[left_index].x);
    float dy = 0.5 * (PostInputTex[up_index].z - PostInputTex[down_index].z);
    float3 normal = float3(0, 1, 0) - float3(dx, 0, dy);
    normal = normalize(normal);
    NormalResTex[id.xy] = float4(normal.x, normal.y, normal.z, 1);
}
